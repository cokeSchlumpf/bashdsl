grammar com.wellnr.BashDSL with org.eclipse.xtext.common.Terminals

generate bashDSL "http://www.wellnr.com/BashDSL"

Domainmodel:
  '@GenerateReadme(' readmeTitle = STRING (',' readmeIntro = STRING)? ')'
  (elements += Script)*
;
 
Script:
  'script' name = ID description = STRING ('copyright' copyright = STRING)? '{'
    (longDescription = Description)?
    (variables += EnvironmentVariable)*
    (arguments += Argument)*
    (optArguments += OptionalArgument)*
    
    (functions += Function)*
    
    code = CODE_BLOCK
  '}'
;

Description:
  "description" value = CODE_BLOCK
;

AbstractArgument:
	Argument | OptionalArgument
;

Argument:
  'argument' name = STRING (remaining ?= 'reads remaining')? description = STRING
;

OptionalArgument:
  'optional argument' name = STRING (('with' (dynamicDefault ?= 'dynamic')? 'default' default = STRING | remaining ?= 'reads remaining') | isBoolean ?= 'without parameter')? description = STRING
;

EnvironmentVariable:
  'env' name = ID ('with' (dynamicDefault ?= 'dynamic')? 'default' default = STRING)? description = STRING
;

Function:
  'def' name = ID code = CODE_BLOCK
;

terminal ID:
  ('a'..'z'|'A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'-'|'0'..'9')*
;

terminal CODE_BLOCK:
	'{{{' -> '}}}'
;
